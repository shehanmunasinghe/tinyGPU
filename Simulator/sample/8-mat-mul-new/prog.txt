#R0     is
#R1	    i
#R2	    j
#R3	    k
#R4	    L
#R5	    M
#R6	    N
#R7	    A
#R8	    B
#R9	    C
#R10	temp
#R11	Aij
#R12	Bjk
#R13	T1
#R14	T2
#R15 	T3

#Load L,M,N,A,B,C
LOADI R10 0
LOAD R4 R10 #L

LOADI R10 1
LOAD R5 R10 #M

LOADI R10 2
LOAD R6 R10 #N

LOADI R10 3
LOAD R7 R10 #A

LOADI R10 4
LOAD R8 R10 #B

LOADI R10 5
LOAD R9 R10 #C

CLEAR R0        #is=0
L1: NOP

    LOADC R1 THRD_ID    #i=THRD_ID
    ADD R1 R1 R0        #i=THRD_ID+is   #R1<=R1+R0
    
    CLEAR R3    #k=0
    L2: NOP
        SETP LT R1 R4   #if i<L
        IF_P ENDIF1
            CLEAR R10

            CLEAR R2
            L3: MUL R15 R5 R1   #R15<=M*i
                ADD R15 R15 R2  #R15+=j
                ADD R15 R15 R7  #R15+=A
                LOAD R11 R15     #R11=M[A+M*i+j] #Loading Aij

                MUL R15 R6 R2   #R15<=N*j
                ADD R15 R15 R3  #R15+=k
                ADD R15 R15 R8  #R15+=B
                LOAD R12 R15     #R12=M[B+N*j+k] #Loading Bjk

                MAD R10 R11 R12
                
                INC R2
                SETP LT R2 R5
                WHILE_P L3
            ENDWHILE  

            MUL R15 R6 R1   #R15<=N*i
            ADD R15 R15 R3  #R15+=k
            ADD R15 R15 R9  #R15+=C
            STORE R10 R15     #R10=>>M[C+N*i+k] #Storing Cik

        ENDIF1: ENDIF

        INC R3
        SETP LT R3 R6
        WHILE_P L2
    ENDWHILE

    LOADC R13 N_CORES       #Number of SP_Cores
    ADD R0 R0 R13           #R0+=N_CORES #is+=N_CORES
    SETP LT R0 R4
    WHILE_P L1
ENDWHILE

